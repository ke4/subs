buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.3.RELEASE")
        classpath("org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3")
        classpath("net.saliman:gradle-cobertura-plugin:2.3.2")
        classpath 'org.owasp:dependency-check-gradle:1.4.5'
        classpath 'org.hidetake:gradle-ssh-plugin:1.1.3'
    }
}



allprojects {
    ext.version_base = '1.0.0-SUBS-334-SNAPSHOT'
    version = version_base

    apply plugin: 'net.saliman.cobertura'
    apply plugin: "com.github.kt3k.coveralls"
    apply plugin: 'org.owasp.dependencycheck'

    cobertura.coverageFormats = ['html', 'xml'] // coveralls plugin depends on xml format report
    cobertura.coverageIgnoreTrivial = true // do not worry about getters and setters
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'eclipse'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'maven-publish'
    apply plugin: 'org.hidetake.ssh'

    group = 'uk.ac.ebi.subs'

    dependencies {
        compile("org.springframework.boot:spring-boot-devtools")
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '3.3'
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task sourceJar(type: Jar) { from sourceSets.main.allJava }

    if (project.hasProperty('artifactoryUsername')) {
        publishing.repositories.maven.credentials.username = artifactoryUsername
    }

    if (project.hasProperty('artifactoryPassword')) {
        publishing.repositories.maven.credentials.password = artifactoryUsername
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourceJar { classifier "sources" }
            }
        }
        repositories {
            maven {
                if(project.version.endsWith('-SNAPSHOT')) {
                    url "http://ena-dev:8081/artifactory/libs-snapshot-local"
                } else {
                    url "http://ena-dev:8081/artifactory/libs-release-local"
                }
            }
        }
    }



}